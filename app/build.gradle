apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1
    }
}
def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tag', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return "in Develop"
    }
}
android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.sanlorng.classsample"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(KEY_STORE_FILE)
            storePassword KEY_STORE_PASS
            keyAlias KEY_STORE_ALIAS
            keyPassword KEY_PASS
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
}
final String appcompact_version = "1.1.0-alpha03"
final String material_version = "1.1.0-alpha04"
final String core_ktx_version = "1.1.0-alpha05"
final String navigation_ktx_version = "1.0.0"
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$appcompact_version"
    implementation "com.google.android.material:material:$material_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation "android.arch.navigation:navigation-fragment-ktx:$navigation_ktx_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$navigation_ktx_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation project(':kit')
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'androidx.media:media:1.0.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"
    implementation 'com.github.bumptech.glide:glide:4.9.0'
}
